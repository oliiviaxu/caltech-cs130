//=============================================================================
// Caltech CS130 - Winter 2023

%import common.WS
%ignore WS

//========================================
// Top-level formulas and expressions

?formula : "=" expression

?expression : compare_expr | concat_expr | add_expr

//========================================
// Arithmetic expressions

?add_expr : (add_expr ADD_OP)? mul_expr

?mul_expr : (mul_expr MUL_OP)? unary_op

?unary_op : ADD_OP? base

//========================================
// String concatenation

?concat_expr : (concat_expr "&")? base

//========================================
// Function calls

?function_call : FUNCTION_NAME "(" args ")"

args : (expression ("," expression)*)?

//========================================
// Comparison expressions

?compare_expr : (compare_expr COMPARE_OP)? expression

//========================================
// Base values

?base : cell
      | cell_range              -> cell_range
      | ERROR_VALUE             -> error
      | NUMBER                  -> number
      | STRING                  -> string
      | BOOLEAN                 -> boolean
      | "(" expression ")"      -> parens
      | function_call           -> function

//========================================
// Cell and cell-range references

// Single cell reference
cell : (_sheetname "!")? CELLREF

// Cell-range reference
cell_range : (_sheetname "!")? CELLREF ":" CELLREF

// Sheet name (quoted or unquoted)
_sheetname : SHEET_NAME | QUOTED_SHEET_NAME

//========================================
// Lexer terminals

// Specify the operators as separately named lexer rules so that they aren't
// discarded from the resulting parse tree.

ADD_OP: ("+" | "-")
MUL_OP: ("*" | "/")
COMPARE_OP: "=" | "==" | "<>" | "!=" | ">" | "<" | ">=" | "<="

// Error values are also separately named lexer rules so we know what kind of
// error is referenced.  All are case-insensitive.

ERROR_VALUE: ("#ERROR!"i | "#CIRCREF!"i | "#REF!"i | "#NAME?"i | "#VALUE!"i | "#DIV/0!"i)

// Lexer rules for different kinds of terminals

// CELLREF: /[A-Za-z]+[1-9][0-9]*/
CELLREF: /\$?[A-Za-z]+\$?[1-9][0-9]*/

// Unquoted sheet names cannot contain spaces, and are otherwise very simple.
SHEET_NAME: /[A-Za-z_][A-Za-z0-9_]*/

// Quoted sheet names can contain spaces and other interesting characters.  Note
// that this lexer rule also matches invalid sheet names, but that isn't a big
// deal here.
QUOTED_SHEET_NAME: /\'[^']*\'/

// Don't need to support signs on numbers because we have unary +/- operator
// support in the parser.
NUMBER: /([0-9]+(\.[0-9]*)?)|(\.[0-9]+)/

STRING: /\"[^"]*\"/

BOOLEAN: /(?i:true|false)/

// Function names
FUNCTION_NAME: /(?i:[A-Za-z][A-Za-z0-9_]*)/